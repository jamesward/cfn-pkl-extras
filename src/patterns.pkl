module `cfn-pkl-extras`.patterns

import "@cfn/cloudformation.pkl"
import "@cfn/aws/route53/recordset.pkl"
import "@cfn/aws/route53/hostedzone.pkl"
import "@cfn/aws/cloudfront/distribution.pkl"
import "@cfn/aws/cloudfront/function.pkl" as cloudfrontFunction
import "@cfn/aws/certificatemanager/certificate.pkl"
import "route53.pkl"


// todo: must not start with a number?
const resourceNameClases = "A-Za-z0-9"
const resourceNameRegex = Regex("[\(resourceNameClases)]+")
const resourceNameInvalidRegex = Regex("[^\(resourceNameClases)]")

typealias ResourceName = String(matches(resourceNameRegex), length < 256)


class BasicRedirect {
  sub: String?
  to: Uri
  permanent: Boolean = false
  aliases: Listing<route53.ValidDomainName>
  preservePath: Boolean = false
}


// todo: can't have ghpages and redirect
class DomainDetails {
  authCode: (String | Mapping)?
  redirect: BasicRedirect?
  ghpages: String?
  records: Listing<route53.DomainRecord>
  nameServers: Listing<route53.ValidDomainName>
  autoRenew: Boolean = true
}

class ExternalDomainDetails {
  redirect: BasicRedirect?
  records: Listing<route53.DomainRecord>
}

class Domains {
  contact: route53.Contact
  domains: Mapping<route53.ApexDomainName, DomainDetails>

  function apexDomainToDomain(apexDomain: route53.ApexDomainName, domainDetails: DomainDetails): route53.Domain =
    let (_contact = contact)
      new route53.Domain {
        domainName {
          name = apexDomain
        }
        contact = _contact
        transferAuthCode = domainDetails.authCode
        // todo: the way we set the nameservers is brittle
        nameServers = if (domainDetails.nameServers.isEmpty && (domainDetails.redirect != null || domainDetails.ghpages != null || !domainDetails.records.isEmpty))
            cloudformation.GetAtt("\(domainName.apexResourceName)HostedZone", "NameServers")
          else
            domainDetails.nameServers
        autoRenew = domainDetails.autoRenew
      }

  function apexDomainToResources(apexDomain: route53.ApexDomainName, domainDetails: DomainDetails): Mapping<ResourceName, cloudformation.Resource> =
    if (domainDetails.redirect != null)
      new {
        ...apexDomainToDomain(apexDomain, domainDetails).resources
        ...new WebSiteRedirect {
          domainName {
            name = domainDetails.redirect?.sub.ifNonNull((sub) -> "\(sub).\(apexDomain)") ?? apexDomain
          }
          redirect = domainDetails.redirect
          validationMethod = if (domainDetails.authCode != null) "EMAIL" else "DNS" // transferring domains takes a long time, blocking certification creation
        }.resources
        when (!domainDetails.records.isEmpty) {
          ...route53.domainRecords(apexDomain, domainDetails.records)
        }
      }
    else if (domainDetails.ghpages != null)
      new {
        ...apexDomainToDomain(apexDomain, domainDetails).resources
        ...route53.hostedZone(new route53.DomainName { name = apexDomain })
        ...route53.domainRecords(apexDomain, new Listing<route53.DomainRecord> {
          new {
            type = "A"
            values {
              "185.199.108.153"
              "185.199.109.153"
              "185.199.110.153"
              "185.199.111.153"
            }
          }
          new {
            type = "AAAA"
            values {
              "2606:50c0:8000::153"
              "2606:50c0:8001::153"
              "2606:50c0:8002::153"
              "2606:50c0:8003::153"
            }
          }
          new {
            sub = "www"
            type = "CNAME"
            values {
              domainDetails.ghpages!! + ".github.io"
            }
          }
        })
      }
    else
      new {
        ...apexDomainToDomain(apexDomain, domainDetails).resources
        when (!domainDetails.records.isEmpty) {
          ...route53.hostedZone(new route53.DomainName { name = apexDomain })
          ...route53.domainRecords(apexDomain, domainDetails.records)
        }
      }


  fixed resources: Mapping<ResourceName, cloudformation.Resource> = new Mapping<ResourceName, cloudformation.Resource> {
    ...route53.domainCustomResource.resources

    for (apexDomain, domainDetails in domains) {
      ...apexDomainToResources(apexDomain, domainDetails)
    }
  }

  // output the hostedzones
  fixed outputs: Mapping<String, cloudformation.Output> = new {
    ...resources
      .toMap()
      .filter((_, resource) -> resource.Type == "AWS::Route53::HostedZone")
      .mapValues((resourceName, resource) -> new cloudformation.Output {
        Description = "\(resource.Properties["Name"])"
        Value = cloudformation.Ref(resourceName)
        Export {
           Name = resourceName
        }
      })
  }
}

const function distributionRecordSets(domainName: route53.DomainName): Mapping<ResourceName, cloudformation.Resource> =
  new Mapping<ResourceName, cloudformation.Resource> {
    ["\(domainName.resourceName)RecordSetA"] = new recordset.RecordSet {
      HostedZoneId = cloudformation.Ref("\(domainName.apexResourceName)HostedZone")
      Name = domainName.name
      TypeProperty = "A"
      AliasTarget {
        // The following HosteZoneId is always used for alias records pointing to CF.
        HostedZoneId = "Z2FDTNDATAQYW2"
        //HostedZoneId = cloudformation.GetAtt("Distribution", "HostedZoneId")
        DNSName = cloudformation.GetAtt("\(domainName.apexResourceName)Distribution", "DomainName")
        EvaluateTargetHealth = false
      }
    }

    ["\(domainName.resourceName)RecordSetAAAA"] = new recordset.RecordSet {
      HostedZoneId = cloudformation.Ref("\(domainName.apexResourceName)HostedZone")
      Name = domainName.name
      TypeProperty = "AAAA"
      AliasTarget {
        // The following HosteZoneId is always used for alias records pointing to CF.
        HostedZoneId = "Z2FDTNDATAQYW2"
        //HostedZoneId = cloudformation.GetAtt("Distribution", "HostedZoneId")
        DNSName = cloudformation.GetAtt("\(domainName.apexResourceName)Distribution", "DomainName")
        EvaluateTargetHealth = false
      }
    }
  }

typealias ValidationMethod = "DNS" | "EMAIL"

class WebSiteRedirect {

  domainName: route53.DomainName
  redirect: BasicRedirect?
  validationMethod: ValidationMethod

  fixed statusCode = if (redirect.permanent) 301 else 302
  fixed statusDescription = if (redirect.permanent) "Moved Permanently" else "Found"

  fixed postStatusCode = if (redirect.permanent) 308 else 307
  fixed postStatusDescription = if (redirect.permanent) "Permanent Redirect" else "Temporary Redirect"

  fixed redirector: String =
    if (redirect.preservePath)
      """
      function handler(event) {
        var path = event.request.uri
        var statusCode = (event.request.method === 'POST') ? \(postStatusCode) : \(statusCode)
        var statusDescription = (event.request.method === 'POST') ? '\(postStatusDescription)' : '\(statusDescription)'

        return {
          statusCode: statusCode,
          statusDescription: statusDescription,
          headers: {
            'location': { value: '\(redirect.to)' + path }
          }
        };
      }
      """
    else
      """
      function handler(event) {
        var statusCode = (event.request.method === 'POST') ? \(postStatusCode) : \(statusCode)
        var statusDescription = (event.request.method === 'POST') ? '\(postStatusDescription)' : '\(statusDescription)'

        return {
          statusCode: statusCode,
          statusDescription: statusDescription,
          headers: {
            'location': { value: '\(redirect.to)' }
          }
        };
      }
      """

  fixed resources: Mapping<ResourceName, cloudformation.Resource> = new {
    ...route53.hostedZone(domainName)

    ...distributionRecordSets(domainName)

    for (alias in redirect.aliases) {
      ...distributionRecordSets(new route53.DomainName { name = alias })
    }

    ["\(domainName.apexResourceName)Certificate"] = new certificate.Certificate {
      DomainName = domainName.name
      ValidationMethod = validationMethod
      // Adds the validation record to the hostedzone
      DomainValidationOptions {
        new {
          DomainName = domainName.name
          when (validationMethod == "DNS") {
            HostedZoneId = cloudformation.Ref("\(domainName.apexResourceName)HostedZone")
          }
          when (validationMethod == "EMAIL") {
            ValidationDomain = domainName.apex
          }
        }
        for (alias in redirect.aliases) {
          new {
            DomainName = alias
            when (validationMethod == "DNS") {
              HostedZoneId = cloudformation.Ref("\(domainName.apexResourceName)HostedZone")
            }
            when (validationMethod == "EMAIL") {
              ValidationDomain = domainName.apex
            }
          }
        }
      }
      SubjectAlternativeNames = redirect.aliases
    }

    ["\(domainName.apexResourceName)DistributionFunction"] = new cloudfrontFunction.Function {
      Name = "\(domainName.apexResourceName)Redirect"
      AutoPublish = true
      FunctionCode = redirector
      FunctionConfig {
        Comment = "Redirect-All"
        Runtime = "cloudfront-js-1.0"
      }
    }

    ["\(domainName.apexResourceName)Distribution"] = new distribution.Distribution {
      DistributionConfig {
        Enabled = true
        DefaultCacheBehavior {
          ViewerProtocolPolicy = "redirect-to-https"
          TargetOriginId = "dummy-origin"
          FunctionAssociations {
            new {
              EventType = "viewer-request"
              FunctionARN = cloudformation.GetAtt("\(domainName.apexResourceName)DistributionFunction.FunctionMetadata", "FunctionARN")
            }
          }
          ForwardedValues {
            QueryString = false
          }
          AllowedMethods {
            "HEAD"
            "DELETE"
            "POST"
            "GET"
            "OPTIONS"
            "PUT"
            "PATCH"
          }
        }
        Origins {
          new {
            Id = "dummy-origin"
            DomainName = domainName.name
            CustomOriginConfig {
              OriginProtocolPolicy = "https-only"
            }
          }
        }
        HttpVersion = "http2"
        Aliases {
          domainName.name
          ...redirect.aliases
        }
        ViewerCertificate {
          AcmCertificateArn = cloudformation.Ref("\(domainName.apexResourceName)Certificate")
          SslSupportMethod = "sni-only"
        }
      }
    }
  }
}


class ExternalDomains {

  domains: Mapping<route53.ApexDomainName, ExternalDomainDetails>

  function apexDomainToResources(apexDomain: route53.ApexDomainName, externalDomainDetails: ExternalDomainDetails): Mapping<ResourceName, cloudformation.Resource> =
    if (externalDomainDetails.redirect != null)
      new {
        ...new WebSiteRedirect {
          domainName {
            name = externalDomainDetails.redirect?.sub.ifNonNull((sub) -> "\(sub).\(apexDomain)") ?? apexDomain
          }
          redirect = externalDomainDetails.redirect
          validationMethod = "DNS"
        }.resources
        when (!externalDomainDetails.records.isEmpty) {
          ...route53.domainRecords(apexDomain, externalDomainDetails.records)
        }
      }
    else
      new {
        when (!externalDomainDetails.records.isEmpty) {
          ...route53.hostedZone(new route53.DomainName { name = apexDomain })
          ...route53.domainRecords(apexDomain, externalDomainDetails.records)
        }
      }

  hidden resources: Mapping<ResourceName, cloudformation.Resource> =
    new Mapping<ResourceName, cloudformation.Resource> {
      for (apexDomainName, externalDomainDetails in domains) {
        ...apexDomainToResources(apexDomainName, externalDomainDetails)
      }
    }

  // output the hostedzones
  fixed outputs: Mapping<String, cloudformation.Output> = new {
    ...resources
      .toMap()
      .filter((_, resource) -> resource.Type == "AWS::Route53::HostedZone")
      .mapValues((resourceName, resource) -> new cloudformation.Output {
      Description = "\(resource.Properties["Name"])"
      Value = cloudformation.Ref(resourceName)
      Export {
        Name = resourceName
      }
    })
  }
}
